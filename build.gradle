plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
    id 'application'
}

project.setVersion("0.0.6")

repositories {   // repositories for Jar's you access in your code
    jcenter()
    mavenCentral()
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
}

application {
    mainClass = 'com.bob.jr.BobJr'
}

tasks.create('dockerBuild').dependsOn(shadowJar).doLast({
    exec {
        executable 'docker'
        args 'build', '--build-arg', 'PROJECT_VERSION=' + project.getVersion(),
                '-t', 'taclayt2/bob-jr-lava:' + project.getVersion(), '.'
    }
})

tasks.create('dockerPublish').dependsOn(dockerBuild).doLast({
    exec {
        executable 'docker'
        args 'image', 'push', 'taclayt2/bob-jr-lava:' + project.getVersion()
    }
})

dependencies {
    implementation platform('com.google.cloud:libraries-bom:20.9.0')
    implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.32'
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '6.6'
    implementation group: 'com.discord4j', name: 'discord4j-core', version: '3.2.4'
    implementation group: 'com.sedmelluq', name: 'lavaplayer', version: '1.3.76'
    implementation group: 'com.google.cloud', name: 'google-cloud-texttospeech', version: '1.4.1'
    implementation 'com.google.cloud:google-cloud-logging-logback:0.123.4-alpha'

    implementation 'com.google.cloud:google-cloud-secretmanager'

// Add your dependencies here.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

shadowJar {
    mergeServiceFiles()

    // add sound files via the Dockerfile process
    exclude "/soundFiles/*"
}

test {
    useJUnitPlatform()
}